@tool

class_name PickUpComponent extends Node

@export var pickup_distance : Vector3 = Vector3(0,0,-1)

var parent
var object : Node3D
var picked_up : bool = false

const pickup_lerp : float = 0.3

func update_state(interactable: Node3D) -> void:
	if picked_up:
		if Global.player:
			Global.player.remove_collision_exception_with(object)
		picked_up = false
		object = null
		interactable.freeze = false
	else:
		object = interactable
		if Global.player:
			Global.player.add_collision_exception_with(interactable)
		interactable.freeze = true
		picked_up = true

func _physics_process(_delta: float) -> void:
	if picked_up:
		var camera_transform = Global.player.camera.global_transform
		object.global_transform = object.global_transform.interpolate_with(camera_transform.translated_local(pickup_distance), pickup_lerp)

func _ready() -> void:
	parent = get_parent()
	if parent is InteractionComponent:
		parent.player_interacted.connect(update_state)

func _get_configuration_warnings() -> PackedStringArray:
	if parent is not InteractionComponent:
		return ["This node must have a InteractionComponent parent."]
	else:
		return []

func _notification(what: int) -> void:
	if what == NOTIFICATION_ENTER_TREE:
		parent = get_parent()
		update_configuration_warnings()
