class_name InteractionComponent extends Node

signal player_interacted(object)

@export var mesh : MeshInstance3D
@export var context : String
@export var override_icon : bool
@export var new_icon : Texture2D

var parent
var highlight_material = preload("res://assets/materials/interactable_highlight.tres")

func _ready() -> void:
	parent = get_parent()
	connect_parent()
	set_default_mesh()

func _process(_delta: float) -> void:
	pass

func in_range() -> void:
	mesh.material_overlay = highlight_material
	MessageBus.interaction_focused.emit(context, new_icon, override_icon)

func not_in_range() -> void:
	mesh.material_overlay = null
	MessageBus.interaction_unfocused.emit()

func on_interact() -> void:
	player_interacted.emit(parent)

func connect_parent() -> void:
	parent.add_user_signal("focused")
	parent.add_user_signal("unfocused")
	parent.add_user_signal("interacted")
	parent.connect("focused", Callable(self, "in_range"))
	parent.connect("unfocused", Callable(self, "not_in_range"))
	parent.connect("interacted", Callable(self, "on_interact"))

func set_default_mesh() -> void:
	if mesh:
		pass
	else:
		for i in parent.get_children():
			if i is MeshInstance3D:
				mesh = i
